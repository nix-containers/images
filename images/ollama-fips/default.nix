{ mkImage, pkgs, lib, ... }:

# Ollama (fips) - Run large language models locally
# https://ollama.ai/
# Packaged from the upstream prebuilt release bundle (bin/ollama + bundled
# CUDA runner libs), which tracks the latest version without the source
# build's version-specific llama.cpp patches.

let
  version = "0.32.0";
  ollama = pkgs.stdenv.mkDerivation {
    pname = "ollama-fips";
    inherit version;
    src = pkgs.fetchurl {
      url = "https://github.com/ollama/ollama/releases/download/v${version}/ollama-linux-amd64.tar.zst";
      hash = "sha256-VjYtdgnfqeNarrt8nKslYF2PBSjsPV1YXcg9ZkIAK6s=";
    };
    nativeBuildInputs = [ pkgs.zstd pkgs.autoPatchelfHook ];
    buildInputs = [ pkgs.stdenv.cc.cc.lib ];
    # GPU libs resolve libcuda/nvidia-ml from the host at runtime.
    autoPatchelfIgnoreMissingDeps = true;
    unpackPhase = ''
      runHook preUnpack
      mkdir -p src
      tar --use-compress-program=unzstd -xf "$src" -C src
      runHook postUnpack
    '';
    sourceRoot = "src";
    installPhase = ''
      runHook preInstall
      mkdir -p "$out"
      cp -r bin lib "$out"/
      runHook postInstall
    '';
  };
in
mkImage {
  drv = ollama;
  name = "ollama-fips";
  tag = "v${version}";
  entrypoint = [ "${ollama}/bin/ollama" ];
  cmd = [ "serve" ];

  extraPkgs = with pkgs; [ cacert ];

  labels = {
    "org.opencontainers.image.title" = "Ollama";
    "org.opencontainers.image.description" = "Get up and running with large language models locally";
    "org.opencontainers.image.version" = version;
  };
}
